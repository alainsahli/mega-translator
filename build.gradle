apply plugin: 'scala'

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.scala-lang:scala-library:2.10.0'
  testCompile 'org.scalatest:scalatest_2.10:1.9.1'
}

tasks.withType(ScalaCompile) {
  // use zync-based incremental compiler
  scalaCompileOptions.useAnt = false
}

task scalaTest(dependsOn: testClasses) {
  inputs.files sourceSets.main.output.classesDir
  inputs.files sourceSets.test.output.classesDir
  outputs.files testResultsDir

  doLast {
    ant.taskdef(name: 'scalatest',
            classname: 'org.scalatest.tools.ScalaTestAntTask',
            classpath: sourceSets.test.runtimeClasspath.asPath
    )
    ant.scalatest(runpath: sourceSets.test.compileClasspath,
            fork: 'false',
            haltonfailure: 'true',
            suite: 'TranslatorTest'
    ) {
      reporter(type: 'stdout')
      reporter(type: 'junitxml', directory: testResultsDir)
    }
  }
}

check.dependsOn scalaTest

task wrapper(type: Wrapper) {
  description = 'Generates gradlew executables'
  gradleVersion = '1.4'
}
